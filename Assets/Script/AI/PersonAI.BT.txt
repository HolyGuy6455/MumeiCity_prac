// 뿌리 트리
tree("Root")
	fallback
		sequence
			not AmIAdult
			Grow
			Wait 1.0
		tree("DayWork")
		tree("NightSleep")

// 밤에 휴식하기
tree("NightSleep")
	while not IsItDayTime // 밤이예요?
		fallback
			sequence
				DoIHaveItemMoreThan 0
				tree("PutInItem") // 아이템 정리하고 마무리
			fallback
				sequence
					AmISleep			// 자고있었니?
					Wait 4.0			// 그럼 깨우지 말아줘
					SleepAct			// zzZ
				sequence		
					GoToHome			// 집으로 가야지
					not MoveToDestination // 간다
				sequence				// 행복이 떨어졌으면 선물로 회복
					not AmIHappyToWork
					TakeOutByTag "Gift"
					ConsumeItemByTag "Gift"
				sequence				// 기력이 떨어졌으면 밥으로 회복
					not DoIHaveStaminaMoreThan 900
					TakeOutByTag "Food"
					ConsumeItemByTag "Food"
				sequence
					SleepAct			// zzZ
					MakeResultOfLove	// 성공하든 실패하든 기회는 하루에 한번뿐

// 낮에 하는 일
tree("DayWork")
	while IsItDayTime // 낮이예요?
		fallback
			AwakeAct	// 일단 잠에서 깨어납시다
			sequence
				Wait 0.2
				LookForHouse
			sequence
				while IsMyJob "Jobless"
					LookForJob		// 직업이 없으니 탐색한다...
			sequence
				not AmIHappyToWork	// 오늘 일하기 싫은가?
				GoToHome			// 집에
				MoveToDestination	// 가서
				Wait 4.0			// 멍이나 때려야지
			sequence
				DoIHaveItemMoreThan 5 	// 아이템이 꽉차면
				tree("PutInItem")		// 아이템을 직장에 넣는다
			// 각 직업에 맞는 일을 한다
			tree("Forester")	// 숲지기의 일을 한다
			tree("Blacksmith")		// 대장간 일을 한다
			tree("Chef")		// 식당 일을 한다
			tree("Farmer")	// 농부 일을 한다
			tree("Miner")	// 광부의 일을 한다
			tree("Cleric")		// 성직자 일을 한다
			tree("Tailor")
			Wait 1.0

tree("PutInItem")
	sequence
		GoPutInItem			// 아이템 넣으러 가야지
		MoveToDestination	// 간다
		while DoIHaveItemMoreThan 0	// 아이템이 1개 이상이면
			repeat Wait 1.0			// 아이템을 다 넣을때까지 기다린다
			// 아이템을 넣는 스크립트가 없고 기다리기만 하는 이유는
			// 아이템 넣는 스크립트가 애니메이션 쪽에 있기 때문
			// 애니메이션 행동과 모션을 맞추려다보니 이렇게 되었음

tree("Forester")	// 숲지기
	while IsMyJob "Forester"	// 직업이 맞는지 확인
		fallback
			sequence	// 식물관련 아이템을 줍는다
				SearchItemToPickUp "Plant"	
				MoveToDestination
			sequence	// 캘 수 있는 나무를 찾는다
				ChangeTool "Axe"
				SearchingToGathering "Log"	
				MoveToDestination

tree("Blacksmith")			// 대장장이
	while IsMyJob "Blacksmith"	// 직업이 맞는지 확인
		sequence
			tree("PutInItem")	// 직장으로 간다
			DanceAct			// 할게 없다!

tree("Chef")			// 식당 요리사
	while IsMyJob "Chef"	// 직업이 맞는지 확인
		sequence
			tree("PutInItem")	// 직장으로 간다
			DanceAct			// 할게 없다!

tree("Farmer")		// 농부
	while IsMyJob "Farmer"	// 직업이 맞는지 확인
		fallback
			sequence	// 작물 아이템을 줍는다
				SearchItemToPickUp "Crop"
				MoveToDestination
			sequence	// 캘 수 있는 작물을 찾는다
				ChangeTool "Knife"
				SearchingToGathering "Crop"
				MoveToDestination

tree("Miner")		// 광부
	while IsMyJob "Miner"	// 직업이 맞는지 확인
		fallback
			sequence	// 광물 아이템을 줍는다
				SearchItemToPickUp "Mining"
				MoveToDestination
			sequence	// 캘 수 있는 광물을 찾는다
				ChangeTool "Pickaxe"
				SearchingToGathering "Mining"
				MoveToDestination

tree("Cleric")			// 성직자
	while IsMyJob "Cleric"	// 직업이 맞는지 확인
		sequence
			tree("PutInItem")	// 직장으로 간다
			DanceAct			// 할게 없다!

tree("Tailor")			// 성직자
	while IsMyJob "Tailor"	// 직업이 맞는지 확인
		sequence
			tree("PutInItem")	// 직장으로 간다
			DanceAct			// 할게 없다!
