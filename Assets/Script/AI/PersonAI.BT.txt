// 뿌리 트리
tree("Root")
	fallback
		sequence
			not Grow
			Wait 1.0
		tree("DayWork")
		tree("NightSleep")

// 밤에 휴식하기
tree("NightSleep")
	while not IsItDayTime // 밤이예요?
		fallback
			sequence
				DoIHaveItemMoreThan 0
				tree("PutInItem") // 아이템 정리하고 마무리
			sequence		
				GoToHome			// 집으로 가야지
				MoveToDestination	// 간다
				fallback
					sequence
						not AmIHappyToWork
						TakeOutByTag "Gift"
						ConsumeItemByTag "Gift"
						Wait 1.0
					sequence
						not DoIHaveStaminaMoreThan 900
						TakeOutByTag "Food"
						ConsumeItemByTag "Food"
						Wait 1.0
					sequence
						SleepAct			// zzZ
						Wait 1.0

// 낮에 하는 일
tree("DayWork")
	while IsItDayTime // 낮이예요?
		fallback
			AwakeAct	// 일단 잠에서 깨어납시다
			sequence
				Wait 0.2
				LookForHouse
			sequence
				while IsMyJob "Jobless"
					LookForJob		// 직업이 없으니 탐색한다...
			sequence
				not AmIHappyToWork	// 오늘 일하기 싫은가?
				GoToHome			// 집에
				MoveToDestination	// 가서
				Wait 4.0			// 멍이나 때려야지
			sequence
				DoIHaveItemMoreThan 5 	// 아이템이 꽉차면
				tree("PutInItem")		// 아이템을 직장에 넣는다
			// 각 직업에 맞는 일을 한다
			tree("Forester")	// 숲지기의 일을 한다
			tree("Blacksmith")		// 대장간 일을 한다
			tree("Chef")		// 식당 일을 한다
			tree("Farmer")	// 농부 일을 한다
			tree("Miner")	// 광부의 일을 한다
			tree("Cleric")		// 성직자 일을 한다
			Wait 1.0
			
tree("PutInItem")
	sequence
		GoPutInItem			// 아이템 넣으러 가야지
		MoveToDestination	// 간다
		while DoIHaveItemMoreThan 0	// 아이템이 1개 이상이면
			repeat Wait 1.0			// 아이템을 다 넣을때까지 기다린다
			// 아이템을 넣는 스크립트가 없고 기다리기만 하는 이유는
			// 아이템 넣는 스크립트가 애니메이션 쪽에 있기 때문
			// 애니메이션 행동과 모션을 맞추려다보니 이렇게 되었음


tree("Forester")	// 숲지기
	while IsMyJob "Forester"	// 직업이 맞는지 확인
		fallback
			sequence	// 나무 아이템을 줍는다
				SearchItemToPickUp "Log"	
				MoveToDestination
			sequence	// 나무 씨앗 아이템을 줍는다
				SearchItemToPickUp "LogSeed"	
				MoveToDestination
			sequence	// 캘 수 있는 나무를 찾는다
				ChangeTool "Axe"
				SearchingToGathering "Log"	
				MoveToDestination

tree("Blacksmith")			// 대장장이
	while IsMyJob "Blacksmith"	// 직업이 맞는지 확인
		DanceAct		// 할게 없다!

tree("Chef")			// 식당 요리사
	while IsMyJob "Chef"	// 직업이 맞는지 확인
		DanceAct		// 할게 없다!

tree("Farmer")		// 농부
	while IsMyJob "Farmer"	// 직업이 맞는지 확인
		fallback
			sequence	// 광물 아이템을 줍는다
				SearchItemToPickUp "Crop"
				MoveToDestination
			sequence	// 캘 수 있는 광물을 찾는다
				ChangeTool "Knife"
				SearchingToGathering "Crop"
				MoveToDestination

tree("Miner")		// 광부
	while IsMyJob "Miner"	// 직업이 맞는지 확인
		fallback
			sequence	// 광물 아이템을 줍는다
				SearchItemToPickUp "Mining"
				MoveToDestination
			sequence	// 캘 수 있는 광물을 찾는다
				ChangeTool "Pickaxe"
				SearchingToGathering "Mining"
				MoveToDestination

tree("Cleric")			// 성직자
	while IsMyJob "Cleric"	// 직업이 맞는지 확인
		DanceAct		// 할게 없다!

// tree("FoodStorage")	// 식량창고지기.....이젠 직업도 건물도 없어
// 	while IsMyJob "FoodStorage"	// 직업이 맞는지 확인
// 		fallback
// 			sequence	// 음식 아이템을 줍는다
// 				SearchItemToPickUp "Food"
// 				MoveToDestination
// 			sequence	// 캘 수 있는 과일을 찾는다
// 				ChangeTool "Knife"
// 				SearchingToGathering "Fruit"
// 				MoveToDestination